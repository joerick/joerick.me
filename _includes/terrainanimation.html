<div id="terrain-animation">
</div>

<style>
  #terrain-animation {
    position: absolute;
    top: 0; bottom: 0; left: 0; right: 0;
  }
</style>

<script src="https://ajax.googleapis.com/ajax/libs/threejs/r76/three.min.js"></script>

<script id="fragmentShader" type="x-shader/x-fragment">
  varying vec3 vViewPosition;

  float rand(vec2 co){
    return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
  }

  void main() {
    vec3 outgoingLight = vec3(0.0);
    vec3 facetedNormal = normalize(-cross(dFdx(vViewPosition), dFdy(vViewPosition)));

    vec3 smoothNormal = normalize(vViewPosition);

    vec3 combinedNormal = normalize(facetedNormal + smoothNormal);

    float intensity = combinedNormal.z;
    vec3 color = vec3(0.2, 0.5, 0.8);
    color *= 2.5 * intensity;
    color += vec3(0.2, 0.2, 0.2);

    // color += vec3(0.8, 0.5, 0.1) * combinedNormal.x * 0.2;

    float distance = length(vViewPosition);
    vec3 fogColor = vec3(1.0, 1.0, 1.0);
    float fogAmount = distance * 0.00005;
    fogAmount = 1.0 - 1.0 / exp(distance * 0.0002 * distance * 0.0002);
    fogAmount = clamp(fogAmount, 0.0, 1.0);
    color = fogAmount * fogColor + (1.0-fogAmount) * color;

    vec3 noiseColor = vec3(0.5, 0.5, 0.5);
    // "color += noiseColor * clamp(rand(gl_FragCoord.xy) * 3.0 - 1.5, -1.0, 1.0) * 0.05;",
    // "color *= 0.9;",

    gl_FragColor = vec4(color, 1.0);
  }

</script>

<script id="vertexShader" type="x-shader/x-vertex">
  uniform float time;

  uniform vec2 uOffset;

  varying vec3 vViewPosition;

  vec4 permute(vec4 x) {
    return mod(((x * 34.0) + 1.0) * x, 289.0);
  }

  vec4 taylorInvSqrt(vec4 r) {
    return 1.79284291400159 - 0.85373472095314 * r;
  }

  float snoise(vec3 v) {
    const vec2 C = vec2(1.0 / 6.0, 1.0 / 3.0);
    const vec4 D = vec4(0.0, 0.5, 1.0, 2.0);

    // First corner

    vec3 i  = floor(v + dot(v, C.yyy));
    vec3 x0 = v - i + dot(i, C.xxx);

    // Other corners

    vec3 g = step(x0.yzx, x0.xyz);
    vec3 l = 1.0 - g;
    vec3 i1 = min(g.xyz, l.zxy);
    vec3 i2 = max(g.xyz, l.zxy);

    vec3 x1 = x0 - i1 + 1.0 * C.xxx;
    vec3 x2 = x0 - i2 + 2.0 * C.xxx;
    vec3 x3 = x0 - 1. + 3.0 * C.xxx;

    // Permutations

    i = mod(i, 289.0);
    vec4 p = permute(permute(permute(
         i.z + vec4(0.0, i1.z, i2.z, 1.0))
         + i.y + vec4(0.0, i1.y, i2.y, 1.0))
         + i.x + vec4(0.0, i1.x, i2.x, 1.0));

    // Gradients
    // (N*N points uniformly over a square, mapped onto an octahedron.)

    float n_ = 1.0 / 7.0; // N=7

    vec3 ns = n_ * D.wyz - D.xzx;

    vec4 j = p - 49.0 * floor(p * ns.z *ns.z);  //  mod(p,N*N)

    vec4 x_ = floor(j * ns.z);
    vec4 y_ = floor(j - 7.0 * x_);    // mod(j,N)

    vec4 x = x_ *ns.x + ns.yyyy;
    vec4 y = y_ *ns.x + ns.yyyy;
    vec4 h = 1.0 - abs(x) - abs(y);

    vec4 b0 = vec4(x.xy, y.xy);
    vec4 b1 = vec4(x.zw, y.zw);


    vec4 s0 = floor(b0) * 2.0 + 1.0;
    vec4 s1 = floor(b1) * 2.0 + 1.0;
    vec4 sh = -step(h, vec4(0.0));

    vec4 a0 = b0.xzyw + s0.xzyw * sh.xxyy;
    vec4 a1 = b1.xzyw + s1.xzyw * sh.zzww;

    vec3 p0 = vec3(a0.xy, h.x);
    vec3 p1 = vec3(a0.zw, h.y);
    vec3 p2 = vec3(a1.xy, h.z);
    vec3 p3 = vec3(a1.zw, h.w);

    // Normalise gradients

    vec4 norm = taylorInvSqrt(vec4(dot(p0, p0), dot(p1, p1), dot(p2, p2), dot(p3, p3)));
    p0 *= norm.x;
    p1 *= norm.y;
    p2 *= norm.z;
    p3 *= norm.w;

    // Mix final noise value

    vec4 m = max(0.6 - vec4(dot(x0, x0), dot(x1, x1), dot(x2, x2), dot(x3, x3)), 0.0);
    m = m * m;
    return 42.0 * dot(m*m, vec4(dot(p0, x0), dot(p1, x1),
                      dot(p2, x2), dot(p3, x3)));
  }

  float surface3(vec3 coord) {
    float n = 0.0;

    n += 1.0 * abs(snoise(coord));
    n += 0.5 * abs(snoise(coord * 2.0));
    n += 0.25 * abs(snoise(coord * 4.0));
    n += 0.125 * abs(snoise(coord * 8.0));

    return n;
  }

  void main() {
    vec2 uvBase = uv;

    float xShim = -fract(uOffset.x*0.5/5.0/128.0*12000.0)/128.0;

    // disable x shimming
    xShim *= 0.0;

    uvBase.x += xShim;

    vec3 noiseCoordinate = vec3(5.0*uvBase + 0.5*uOffset, 0.0);
    float noise = surface3(noiseCoordinate);
    noise *= 300.0;
    vec3 displacedPosition = normal * noise + position;

    displacedPosition.x += xShim * (12000.0);

    // curve up the surface in a big parabola
    displacedPosition.z += length(displacedPosition)*length(displacedPosition) * 0.00003;

    vec4 worldPosition = modelMatrix * vec4(displacedPosition, 1.0);
    vec4 mvPosition = modelViewMatrix * vec4(displacedPosition, 1.0);

    gl_Position = projectionMatrix * mvPosition;

    vViewPosition = -mvPosition.xyz;
  }
</script>

<script>
  function webGLSupported () {
    try {
      var canvas = document.createElement( 'canvas' )
      return !!(window.WebGLRenderingContext && (canvas.getContext('webgl')
                                                 || canvas.getContext('experimental-webgl')))
    }
    catch (err) {
      return false
    }
  }

  var container, renderer, camera, scene, uniforms, clock, render_width, render_height

  if (webGLSupported()) {
    init()
    animate()
  }

  function init () {
    'use strict'
    container = document.getElementById('terrain-animation')
    render_width = container.offsetWidth
    render_height = container.offsetHeight

    clock = new THREE.Clock()

    // CAMERA

    camera = new THREE.PerspectiveCamera(30, render_width / render_height, 2, 40000)
    camera.position.set(-1200, 380, 1200)
    camera.lookAt(new THREE.Vector3(0, 100, 0))

    // var controls = new THREE.OrbitControls(camera)
    // controls.target.set(0, 0, 0)

    // controls.rotateSpeed = 1.0
    // controls.zoomSpeed = 1.2
    // controls.panSpeed = 0.8

    // controls.keys = [65, 83, 68]

    // SCENE

    scene = new THREE.Scene()

    var vertexShader = document.getElementById('vertexShader').textContent
    var fragmentShader = document.getElementById('fragmentShader').textContent

    // SHADER

    uniforms = {
      'uOffset': { value: new THREE.Vector2(100.0, 0) },
      'time': { value: 0.0 }
    }

    var material = new THREE.ShaderMaterial({
      uniforms: uniforms,
      vertexShader: vertexShader,
      fragmentShader: fragmentShader,
      lights: false,
      fog: false,
      extensions: {derivatives: true},
    })

    // TERRAIN MESH

    var geometryTerrain = new THREE.PlaneBufferGeometry(12000, 12000, 128, 128)
    var terrain = new THREE.Mesh(geometryTerrain, material)
    terrain.position.set(0, -125, 0)
    terrain.rotation.x = -Math.PI / 2
    terrain.visible = true
    scene.add(terrain)

    // RENDERER

    renderer = new THREE.WebGLRenderer()
    renderer.setClearColor(0xffffff)
    renderer.setPixelRatio(window.devicePixelRatio)
    renderer.setSize(render_width, render_height)
    container.appendChild(renderer.domElement)

    // EVENTS

    onWindowResize()

    window.addEventListener('resize', onWindowResize, false)
  }

  function onWindowResize (event) {
    render_width = container.offsetWidth
    render_height = container.offsetHeight

    renderer.setSize(render_width, render_height)

    camera.aspect = render_width / render_height
    camera.updateProjectionMatrix()
  }

  function animate () {
    requestAnimationFrame(animate)

    render()
  }

  function render () {
    var delta = clock.getDelta()

    uniforms['uOffset'].value.x += delta * 0.03

    renderer.render(scene, camera)
  }
</script>